plugins {
    id 'application'
    id 'groovy'
}

group = 'net.ioixd'
version = '1.0-SNAPSHOT'

compileJava {
    options.compilerArgs += ["-h", file("include")]
}

application {
    // Define the main class for the application.
    mainClass = 'net.ioixd.spigotjsongen.App'
    applicationDefaultJvmArgs += "-Djava.library.path=${projectDir}/lib/"
}

repositories {
    mavenCentral()
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

        // As of Gradle 5.1, you can limit this to only those
        // dependencies you expect from it
        content {
        includeGroup 'org.bukkit'
        includeGroup 'org.spigotmc'
        }
    }
    /*
    As Spigot-API depends on the BungeeCord ChatComponent-API,
    we need to add the Sonatype OSS repository, as Gradle,
    in comparison to maven, doesn't want to understand the ~/.m2
    directory unless added using mavenLocal(). Maven usually just gets
    it from there, as most people have run the BuildTools at least once.
    This is therefore not needed if you're using the full Spigot/CraftBukkit,
    or if you're using the Bukkit API.
    */
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://oss.sonatype.org/content/repositories/central' }
    // mavenLocal() // This is needed for CraftBukkit and Spigot.
}

dependencies {
    implementation "org.spigotmc:spigot-api:1.20.1-R0.1-SNAPSHOT"
    implementation "org.reflections:reflections:0.10.2"
    implementation "org.javassist:javassist:3.29.2-GA"
    implementation "io.github.classgraph:classgraph:4.8.160"

    implementation "org.jetbrains:annotations:24.0.1"

    implementation "com.google.code.gson:gson:2.10.1"
    implementation "org.slf4j:slf4j-api:2.0.7"
    implementation "org.eclipse.aether:aether-api:1.1.0"

    implementation 'org.jsoup:jsoup:1.16.1'

    implementation 'org.codehaus.groovy:groovy-json:3.0.9'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

jar {
  manifest {
    attributes(
      'Class-Path': configurations.runtimeClasspath.files.collect { it.getName() }.join(' '),
      'Main-Class': 'net.ioixd.spigotjsongen.App'
    )
  }
}

def rustBasePath = "rust"

// execute cargo metadata and get path to target directory
tasks.create(name: "cargo-output-dir", description: "Get cargo metadata") {
    new ByteArrayOutputStream().withStream { os ->
        exec {
            commandLine 'cargo', 'metadata', '--format-version', '1'
            workingDir rustBasePath
            standardOutput = os
        }
        def outputAsString = os.toString()
        def json = new groovy.json.JsonSlurper().parseText(outputAsString)

        logger.info("cargo target directory: ${json.target_directory}")
        project.ext.cargo_target_directory = json.target_directory
    }
}

// Build with cargo
tasks.create(name: "cargo-build", type: Exec, description: "Running Cargo build", dependsOn: "cargo-output-dir") {
    workingDir rustBasePath
    commandLine 'cargo', 'build', '--release'
}

tasks.create(name: "rust-deploy", type: Sync, dependsOn: "cargo-build") {
    from "${project.ext.cargo_target_directory}/release"
    include "*.dylib","*.so"
    into "lib/"
}

clean.dependsOn "clean-rust"
tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn "rust-deploy"
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'rust'
        }
    }
}
